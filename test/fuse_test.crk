
import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.exp.fuse Context;
import crack.io cerr, cout, StringReader, StringWriter;
import crack.lang Buffer, ManagedBuffer;
import crack.protobuf readMessageFromString;
import crack.runtime free, Stat;
import mawfs.blockstore Entry, FSInfo, Node, NodeStore, MODE_DIR, MODE_EXE;
import mawfs.fuse BlockIter, MAWFSNode;

@import crack.ann assert, impl;

String readNode(Node node, int pos, int size) {
    ManagedBuffer buf = {size};
    BlockIter iter = {node};
    iter.setPos(pos);
    iter.read(buf);
    return String(buf, true);
}

int writeNode(Node node, int pos, Buffer data) {
    BlockIter iter = {node};
    iter.setPos(pos);
    return iter.write(data);
}

Stat copyStat(Stat other) {
    if (other is null)
        return null;
    Stat result = Stat();
    result.st_dev = other.st_dev;
    result.st_ino = other.st_ino;
    result.st_mode = other.st_mode;
    result.st_nlink = other.st_nlink;
    result.st_uid = other.st_uid;
    result.st_gid = other.st_gid;
    result.st_rdev = other.st_rdev;
    result.st_blksize = other.st_blksize;
    result.st_blocks = other.st_blocks;
    result.st_atime = other.st_atime;
    result.st_mtime = other.st_mtime;
    result.st_ctime = other.st_ctime;

    return result;
}

class Info {
    String filename;
    Stat stat;

    oper init(String filename, Stat stat) :
        filename = filename,
        stat = copyStat(stat) {
    }

    oper del() {
        free(stat);
    }
}

Entry e(String filename) {
    result := Entry();
    result.name = filename;
    return result;
}

Array[Info] filled = {};

int filler(voidptr buf, byteptr filename, Stat stat, uintz something) {
    filled.append(Info(String(filename), stat));
    return 0;
}

class FakeNodeStore @impl NodeStore {
    HashMap[String, String] __contents = {};
    FSInfo __fsInfo = {'12345678123456781234567812345678'};
    String rootDigest;

    oper init() {}

    String storeNode(Node node) {
        StringWriter temp = {};
        digest := __fsInfo.writeChunk(temp, node.toString());
        __contents[digest] = String(temp, true);
        return digest;
    }

    ## Get the node at the given digest, null if it's not currently stored.
    Node getNode(String digest) {
        serialized := __contents[digest];
        chunk := __fsInfo.readChunk(StringReader(serialized));
        node := Node();
        readMessageFromString(node, chunk.contents);
        return node;
    }

    # Get the root node.
    Node getRoot() {
        if (!rootDigest)
            return null;
        return getNode(rootDigest);
    }

    ## Store the digest of the root node.
    void storeRoot(String digest) {
        rootDigest = digest;
    }
}


###### BEGIN TESTS

if (true) {
    node := Node();
    node.contents = 'this is a test';

    iter := BlockIter(node);
    @assert(readNode(node, 0, 4) == 'this');
    @assert(readNode(node, 5, 4) == 'is a');
    @assert(readNode(node, 10, 10) == 'test');
}

if (true) {
    node := Node();
    node.children = Array[Entry]![e('foo'), e('bar')];
    node.contents = 'this is some data';

    MAWFSNode fuseNode = {FakeNodeStore(), node};
    @assert(!fuseNode.readdir(null, null, filler, 0, null));
    @assert(filled[0].filename == '.');
    @assert(filled[1].filename == '..');
    @assert(filled[2].filename == 'foo');
    @assert(filled[3].filename == 'bar');
    @assert(filled[3].stat.st_mode == 040777);

    ManagedBuffer buf = {64};
    @assert(fuseNode.read(null, buf.buffer, buf.cap, 0, null) == 17);
    buf.size = 17;
    @assert(String(buf, true) == 'this is some data');
}

if (true) {
    node := Node();
    MAWFSNode fuseNode = {FakeNodeStore(), node};

    @assert(writeNode(node, 0, 'some data') == 9);
    @assert(readNode(node, 0, 9) == 'some data');
    @assert(writeNode(node, 5, 'pig') == 3);
    @assert(readNode(node, 0, 1024) == 'some piga');
}

if (true) {
    MAWFSNode fs = {FakeNodeStore(), Node()};
    ManagedBuffer buf = {8};
    Context ctx = {'/foo', fs};

    data := 'data';
    @assert(fs.write(ctx, data.buffer, data.size, 0, null) == 4);
    @assert(fs.write(ctx, data.buffer, data.size, 4, null) == 4);
    @assert((buf.size = fs.read(ctx, buf.buffer, 3, 0, null)) == 3);
    @assert(buf == 'dat');
    @assert((buf.size = fs.read(ctx, buf.buffer, 10, 3, null)) == 5);
    @assert(buf == 'adata');
}

# Creating files.
if (true) {
    nodeStore := FakeNodeStore();
    root := Node();
    nodeStore.storeRoot(nodeStore.storeNode(root));
    MAWFSNode fs = {nodeStore, root};
    Context ctx = {'/foo', fs};
    @assert(fs.create(ctx, 0, null) == 0);
    children := root.children;
    @assert(children.count() == 1);
    @assert(children[0].name = 'foo');
    @assert(!nodeStore.getNode(children[0].hash).mode);

    # Verify that we can reproduce this from the node store.
    root = nodeStore.getRoot();
    children = root.children;
    @assert(children.count() == 1);
    @assert(children[0].name == 'foo');

    # Verify that we overwrite the existing child and don't add a new one.
    # (I'm not sure this behavior is correct, I think it might be appropriate
    # to make this case an error and require a truncate).
    @assert(fs.create(ctx, 0, null) == 0);
    @assert(children.count() == 1);
    @assert(children[0].name = 'foo');
}

# Create nested file.
if (true) {
    nodeStore := FakeNodeStore();
    root := Node();
    nodeStore.storeRoot(nodeStore.storeNode(root));
    MAWFSNode fs = {nodeStore, root};
    Context ctx = {'/foo', fs};

    @assert(fs.mkdir(ctx, 0) == 0);
    children := root.children;
    @assert(children.count() == 1);
    @assert(children[0].name == 'foo');

    foo := ctx.lookUp();
    ctx = Context('/foo/bar', fs);
    @assert(foo.create(ctx, 0, null) == 0);

    # Read back all the way from the node store.
    root = nodeStore.getRoot();
    children = root.children;
    @assert(children.count() == 1);
    @assert(children[0].name == 'foo');

    dirNode := nodeStore.getNode(children[0].hash);
    @assert(dirNode.mode == MODE_DIR);

    children = dirNode.children;
    @assert(children.count() == 1);
    @assert(children[0].name == 'bar');
}

# Truncate.
if (true) {
    nodeStore := FakeNodeStore();
    root := Node();
    nodeStore.storeRoot(nodeStore.storeNode(root));
    MAWFSNode fs = {nodeStore, root};
    Context ctx = {'/foo', fs};

    @assert(fs.create(ctx, 0, null) == 0);
    text := 'this is text';
    foo := ctx.lookUp();
    @assert(foo.write(ctx, text.buffer, text.size, 0, null));
    @assert(foo.truncate(ctx, 4) == 0);

    root = nodeStore.getRoot();
    fs.setRep(root);
    ManagedBuffer temp = {10};
    foo = ctx.lookUp();
    rc := temp.size = foo.read(ctx, temp.buffer, temp.cap, 0, null);
    @assert(foo.read(ctx, temp.buffer, temp.cap, 0, null) == 4);

    foo.truncate(ctx, 0);
    root = nodeStore.getRoot();
    fs.setRep(root);
    foo = ctx.lookUp();
    rc = temp.size = foo.read(ctx, temp.buffer, temp.cap, 0, null);
    @assert(foo.read(ctx, temp.buffer, temp.cap, 0, null) == 0);
}

cout `ok\n`;
