# Copyright 2016 Google Inc. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

## A NodeStore implemented in memory.  This is mainly useful for testing.

import crack.cont.array Array;
import crack.cont.hashmap HashMap;
import crack.hash.sha256 SHA256;
import crack.io StringWriter;
import crack.protobuf readMessageFromString, Message, ProtoWriter;
import mawfs.blockstore Change, Chunk, Commit, JournalIter, Node, NodeStore;

@import crack.ann impl;

## A NodeStore implemented entirely in memory.
class MemNodeStore @impl NodeStore {
    HashMap[String, String] __objs = {};
    HashMap[String, Array[Change]] __journal = {};
    HashMap[String, String] __branches = {};
    String __root;

    Chunk __toChunk(Message message) {
        # Construct a digest from the object contents.
        StringWriter temp = {};
        ProtoWriter pw = {temp};
        message.serialize(pw);
        hasher := SHA256();
        serialized := temp.string();
        hasher.update(serialized);
        return Chunk(serialized, hasher.digest());
    }

    String __store(Message message) {
        chunk := __toChunk(message);
        __objs[chunk.digest] = chunk.contents;
        return chunk.digest;
    }

    String storeNode(Node node) {
        return __store(node);
    }

    Node getNode(String digest) {
        serialized := __objs.get(digest);
        if (serialized is null)
            return null;
        Node result = {};
        readMessageFromString(result, serialized);
        return result;
    }

    String storeCommit(Commit commit) {
        return __store(commit);
    }

    Commit getCommit(String digest) {
        serialized := __objs.get(digest);
        if (serialized is null)
            return null;
        Commit result = {};
        readMessageFromString(result, serialized);
        return result;
    }

    Node getRoot() {
        return getNode(__root);
    }

    String getRootDigest() {
        return __root;
    }

    void storeRoot(String digest) {
        __root = digest;
    }

    void setHead(String branch, String head) {
        __branches[branch] = head;
    }

    String getHead(String branch) {
        return __branches.get(branch);
    }

    class __JIter @impl JournalIter {
        Array[Change].ArrayIter __iter;
        oper init(Array[Change].ArrayIter iter) : __iter = iter {}
        Change elem() {
            return __iter ? __iter.elem() : null }
        void next() { if (iter) __iter.next() }
        bool isTrue() { return __iter }
    }

    JournalIter makeJournalIter(String branch) {
        arr := __journal.get(branch);
        return __JIter(arr ? arr.iter() : null);
    }

    void writeToJournal(String branch, Change contents) {
        arr := __journal.get(branch);
        if (!arr)
            __journal[branch] = arr = Array[Change]();
        arr.append(contents);
    }

    void deleteJournal(String digest) {
        if (__journal.hasKey(digest))
            __journal.delete(digest);
    }
}

